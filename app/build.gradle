apply plugin: 'com.android.application'

//FIR发布Apk插件
apply plugin: 'im.fir.plugin.gradle'
fir {
    //必填 上传 fir.im apk 字段，否则无法上传 APP 到 fir.im
    apiToken API_TOKEN

    //可选 上传fir.im 中显示的changelog
    changeLog CHANGE_LOG
}

def buildTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+8:00"))
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.way.capture"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
    }
    signingConfigs {
        release {
            def filePrivateProperties = file("../local.properties")
            if (filePrivateProperties.exists()) {
                Properties propsPrivate = new Properties()
                propsPrivate.load(new FileInputStream(filePrivateProperties))

                storeFile file(propsPrivate['key.store'])
                keyAlias propsPrivate['key.alias']
                storePassword propsPrivate['key.store.password']
                keyPassword propsPrivate['key.alias.password']
            }

        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            //是否开启BUGTAG
            buildConfigField "boolean", "BUGTAG_ENABLED", "false"
            //resValue "string", "bugtag_app_key", BUGTAG_APP_KEY
            buildConfigField "String", "BUGLY_APPID", BUGLY_APP_ID
            signingConfig signingConfigs.release
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //是否开启BUGTAG
            buildConfigField "boolean", "BUGTAG_ENABLED", "true"
            //resValue "string", "bugtag_app_key", BUGTAG_APP_KEY
            buildConfigField "String", "BUGLY_APPID", BUGLY_APP_ID
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //配置生成的文件名
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            if (file != null && file.name.endsWith('release.apk')) {//变种名为以release.apk结尾时才修改名字
                def fileName = file.name.replace(file.name, "Capture_V" + variant.versionName + "_${buildTime()}.apk")
                output.outputFile = new File(file.parentFile, fileName)
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    //compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':firupgrade')
    compile project(':downloadlibrary')
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.alexvasilkov:gesture-views:2.1.1'
    compile 'com.ogaclejapan.arclayout:library:1.0.1@aar'
    compile 'com.thefinestartist:finestwebview:1.2.5'
    compile 'com.github.wooplr:Spotlight:1.1'
    compile 'com.tencent.bugly:crashreport:latest.release' //其中latest.release指代最新版本号，也可以指定明确的版本号，例如2.1.5
    compile 'com.commit451:PhotoView:1.2.5'
    //leakcanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    compile 'io.reactivex:rxandroid:1.2.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.6'
}
