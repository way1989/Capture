apply plugin: 'com.android.application'

def buildTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+8:00"))
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.way.capture"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //multiDexEnabled true
    }

    def filePrivateProperties = file("../jks/public.properties")
    if (filePrivateProperties.exists()) {
        signingConfigs {
            release {
                Properties propsPrivate = new Properties()
                propsPrivate.load(new FileInputStream(filePrivateProperties))

                storeFile file(propsPrivate['key.store'])
                keyAlias propsPrivate['key.alias']
                storePassword propsPrivate['key.store.password']
                keyPassword propsPrivate['key.alias.password']
            }
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "String", "BUGLY_APPID", BUGLY_APP_ID
            if (filePrivateProperties.exists()) {
                signingConfig signingConfigs.release
            }
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "String", "BUGLY_APPID", BUGLY_APP_ID
            if (filePrivateProperties.exists()) {
                signingConfig signingConfigs.release
            }
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //配置生成的文件名
    applicationVariants.all { variant ->//获取 variant 参数，就是 productFlavor x buildType
        variant.outputs.all {
            if(outputFileName.endsWith('-release.apk')) {//变种名以release.apk结尾时才修改名字
                outputFileName = outputFileName.replace(outputFileName, "Capture" + "_V" + variant.versionName + "_${buildTime()}.apk")
            } else if (outputFileName.endsWith('-debug.apk')) {
                outputFileName = outputFileName.replace(outputFileName, "Capture-debug" + "_V" + variant.versionName + "_${buildTime()}.apk")
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation "junit:junit:$rootProject.junitVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation project(':downloadlibrary')
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"

    //google Glide
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    //compile 'com.alexvasilkov:gesture-views:2.1.1'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'com.ogaclejapan.arclayout:library:1.0.1@aar'

    //webview
    implementation 'com.thefinestartist:finestwebview:1.2.7'

    //intro
    //implementation 'com.github.wooplr:Spotlight:1.2.2'
    //bugly
    implementation 'com.tencent.bugly:crashreport:latest.release' //其中latest.release指代最新版本号，也可以指定明确的版本号，例如2.1.5
    //photo view
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    //compile 'com.commit451:PhotoView:1.2.5'
    //leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    //rxjava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    //rxlife
    implementation 'com.trello.rxlifecycle2:rxlifecycle:2.1.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-android:2.1.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'

    //rxbinding
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    //action bar select
    implementation "com.afollestad:material-cab:0.1.12"
    //drag  select recyclerview
    //implementation 'com.afollestad:drag-select-recyclerview:0.4.0'

    //loading more
    //compile 'com.github.markomilos:paginate:0.5.1'

    //butterknife
    implementation "com.jakewharton:butterknife:$rootProject.butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknifeVersion"

    //bitmap pool
    implementation 'com.amitshekhar.android:glide-bitmap-pool:0.0.1'

    //loading layout
    implementation 'com.github.Bakumon:StatusLayoutManager:1.0.1'
    implementation 'com.github.MFlisar:DragSelectRecyclerView:0.3'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.35'
}
